# UD 2 - Procesado y Presentaci√≥n Datos Almacenados - Neo4j

En este cap√≠tulo se profundizar√° en las **bases de datos orientadas a grafos** como soluci√≥n a problemas de Big Data. En la actualidad, cualquier problema puede ser modelizado por medio de grafos. Esta estructura matem√°tica **vers√°til y escalable** proporciona un mayor rendimiento a los sistemas que utilizan este tipo de bases de datos. Para ilustrar todos los conceptos de este cap√≠tulo, se utilizar√° **Neo4j** como plataforma de bases de datos orientada a grafos.

## 1. Bases de datos orientadas a grafos

Una base de datos orientada a grafos es un tipo de base de datos **NoSQL** como lo son tambi√©n, por ejemplo, las bases de datos documentales. Un sistema de gesti√≥n de bases de datos orientadas a grafos es aquel **define m√©todos y operaciones CRUD sobre un modelo de datos representado mediante un grafo**. Por norma general, este tipo de bases de datos se implementan para su uso junto con sistemas OLTP, por lo que est√°n dise√±adas en t√©rminos de relaciones de integridad y optimizadas para ofrecer un rendimiento operacional.

Aunque existen diferentes tipos de modelos de datos basados en grafos, cada sistema de base de datos elegir√° uno de ellos para representar el contenido de la base de datos. Cualquiera de estos modelos est√° inspirado en **la estructura matem√°tica de un grafo para modelizar un conjunto de datos**. Matem√°ticamente, un grafo es un conjunto de **nodos o v√©rtices**, que representan entidades de un dominio, y un conjunto de **aristas o enlaces**, que representan las relaciones o interacciones que se producen entre las entidades. La [figura4.1] muestra un ejemplo de grafo que representa las relaciones de seguimiento entre distintos canales en YouTube.

![figura4.1]

_Figura 4.1: Ejemplo de grafo: relaciones de seguimiento entre canales YouTube_

Uno de los modelos de datos basado en grafos m√°s popular es el **modelo de grafo de propiedades etiquetadas**. Un grafo representado mediante este modelo, debe cumplir las siguientes caracter√≠sticas principales:

1) Debe contener un conjunto de nodos y aristas o relaciones entre dichos nodos,
2) Los nodos contienen propiedades, definidas a trav√©s de pares ‚Äúclave-valor‚Äù,
3) Los nodos pueden estar etiquetados con una o m√°s etiquetas,
4) Las relaciones entre los nodos est√°n nombradas y son dirigidas, teniendo siempre un nodo de inicio y otro nodo de fin,
5) Las relaciones del grafo tambi√©n pueden contener propiedades. Este modelo, a pesar de su simplicidad, es sencillo de entender y permite modelar cualquier problema y/o conjunto de datos.

### 1.1 Caracter√≠sticas principales

El aumento del uso de los sistemas de bases de datos orientados a grafos y el auge que est√°n alcanzando en los √∫ltimos a√±os se debe, principalmente, a una serie de caracter√≠stica que confieren a estos sistemas una serie de ventajas importantes sobre los sistemas de bases de datos tradicionales y otros sistemas NoSQL.

- **Almacenamiento**: Las bases de datos orientadas a grafo pueden ser de almacenamiento nativo, en cuyo caso los datos se almacenan internamente con estructura de grafo. No obstante, existen bases de datos de grafos que mapean los datos para adptarlos a una estructura relacional.
- **Procesamiento**: El almacenamiento nativo permite obtener una estructura de datos que no requiere √≠ndices de adyacencia para referenciar los nodos del grafo, mejorando el rendimiento en la ejecuci√≥n de consultas.
- **Rendimiento**: La ejecuci√≥n de consultas sobre datos estructurados en forma de grafo proporciona escalabilidad al sistema de bases de datos, ya que aunque el n√∫mero de datos aumente, las consultas se ejecutan sobre la porci√≥n del grafo de inter√©s.
- **Flexibilidad**: Los modelos de datos basados en grafos permiten r√°pidamente a√±adir nuevos nodos, enlaces y subgrafos a un modelo existente con facilidad y sin p√©rdida de funcionalidad ni rendimiento.
- **Rapidez**: La naturaleza libre de esquema de las bases de datos orientadas a grafos, juntos con las APIs desarrolladas para su manipulaci√≥n y los lenguajes de consulta, hacen que estos sistemas sean √°giles y r√°pidos, integr√°ndolos con las metodolog√≠as de desarrollo de software iterativas e incrementales.

### 1.2 √Åreas de Aplicaci√≥n

Las caracter√≠sticas de las bases de datos orientadas a grafos desarrolladas en el apartado anterior confieren a esta tecnolog√≠a un sinf√≠n de aplicaciones reales en las que su uso mejora significativamente a las tecnolog√≠as tradicionales. A continuaci√≥n, se muestran algunos ejemplos de aplicaci√≥n donde, actualmente, este tipo de bases de datos es el m√°s ampliamente utilizado.

- **Redes sociales**: El an√°lisis de redes sociales es, a d√≠a de hoy, una de las principales fuentes de informaci√≥n de cualquier dominio. El an√°lisis de redes sociales mediante bases de datos orientadas a grafos permite **identificar relaciones expl√≠citas e impl√≠citas** entre usuarios y grupos de usuarios, as√≠ como identificar la forma en la que ellos interact√∫an pudiendo inferir el comportamiento de un usuario en base a sus conexiones. En una red social, dos usuarios presentan una **conexi√≥n expl√≠cita** si est√°n directamente conectados. Esto ocurre, por ejemplo, entre dos usuarios de Facebook que son amigos o dos compa√±eros de trabajo de la misma empresa en LinkedIn. Por otra parte, una **relaci√≥n impl√≠cita** es aquella que se produce entre dos usuarios a trav√©s de un intermediario, como puede ser otro usuario, un post donde han hecho un comentario, un ‚Äúlike‚Äù o un art√≠culo que ambos han comprado.

- **Sistemas de recomendaci√≥n**: Estos sistemas permiten modelar en forma de grafo las relaciones que se establecen entre personas o usuarios y cosas, como pueden ser productos, servicios, contenido multimedia o cualquier otro concepto relevante en funci√≥n del dominio de aplicaci√≥n. **Las relaciones se establecen en funci√≥n del comportamiento de los usuarios al comprar, consumir contenido, puntuarlo o evaluarlo etc**. De esta forma, los sistemas de recomendaci√≥n **identifican recursos de inter√©s** para un usuario espec√≠fico y pueden **predecir su comportamiento al comprar un producto o contratar un servicio**.

- **Informaci√≥n geogr√°fica**: Se trata del caso de aplicaci√≥n m√°s popular de la teor√≠a de grafos. Las aplicaciones de las bases de datos orientadas a grafos en informaci√≥n geogr√°fica van desde **calcular rutas √≥ptimas entre dos puntos en cualquier tipo de red** (red de carreteras, de ferrocarril, a√©rea, log√≠stica...) **hasta encontrar todos los puntos de inter√©s en un √°rea concreta, encontrar el centro de una regi√≥n u obtener la intersecci√≥n entre dos o m√°s regiones, entre otras muchas**. As√≠, las bases de datos orientadas a grafos permiten modelar estos casos de aplicaci√≥n como grafos dirigidos sobre los cuales operar para obtener los resultados deseados.

Aunque en este apartado se hayan analizado las principales √°reas de aplicaci√≥n, son muchos los dominios de aplicaci√≥n donde las bases de datos orientadas a grafos se est√°n convirtiendo en una soluci√≥n predominante, como lo son **la gesti√≥n de datos maestros, gesti√≥n de redes y centros de datos o control de acceso entre otros muchos**.

_[ver presentaci√≥n BDA4.1](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_01.pdf)_

## 2. Neo4j

En esta secci√≥n se profundiza en una tecnolog√≠a concreta de bases de datos orientadas a grafos. Aunque existe un amplio abanico de tecnolog√≠as que dan soporte a este tipo de bases de datos, como lo son OrientDB1[^1] , FlockDB[^2] o InfiniteGraph[^3] , en esta secci√≥n se profundiza en el trabajo con bases de datos orientadas a grafos utilizando Neo4j[^4] .

[^1]: [https://orientdb.org](https://orientdb.org)
[^2]: [https://github.com/twitter-archive/flockdb](https://github.com/twitter-archive/flockdb)
[^3]: [https://objectivity.com/infinitegraph/](https://objectivity.com/infinitegraph/)
[^4]: [https://neo4j.com](https://neo4j.com)

### 2.1 Introducci√≥n

Neo4j es una plataforma de bases de datos orientada a grafos lanzada en 2007 e implementada en Java. Entre sus caracter√≠sticas principales destacan **el almacenamiento y procesamiento de grafos nativo**, lo que la convierte en una alternativa potente para el trabajo en entornos reales, donde se requiere de estas caracter√≠sticas para mejorar el rendimiento y la escalabilidad de los sistemas. Adem√°s, a pesar de lo anterior, Neo4j tambi√©n **permite trabajar con modelos de datos en forma de grafo de manera transaccional**, por lo que tambi√©n es √∫til en sistemas transaccionales.

Junto a todo lo anterior, Neo4j dispone de **amplias librer√≠as que permiten crear tambi√©n modelos de aprendizaje autom√°tico sobre grafos**. Esta plataforma puede integrarse con m√∫ltiples lenguajes de programaci√≥n como Python o C++, entre otros, y ofrece una serie de herramientas muy √∫tiles para usuarios y desarrolladores. A continuaci√≥n, se enumeran y detallan algunas de ellas:

- **Graph Data Science Library**: Se trata de la librer√≠a principal que contiene una gran cantidad de algoritmos de b√∫squeda sobre grafos adem√°s de modelos de aprendizaje autom√°tico. Se trata de algoritmos y modelos eficientemente programados y escalables para el trabajo con grafos.

- **Neo4j Bloom**: Es una aplicaci√≥n de visualizaci√≥n y exploraci√≥n de grafos que permite la visualizaci√≥n de estos desde distintas perspectivas y puntos de vista, ofreciendo as√≠ una herramienta visual muy √∫til para visualizar los resultados de los algoritmos as√≠ como mostrar resultados a clientes.

- **Cypher**: Es el lenguaje de creaci√≥n de consultas utilizado en Neo4j. Se trata de un lenguaje inspirado en SQL, solo que m√°s sencillo y optimizado para el trabajo con grafos.

- **Integradores**: Con el objetivo de integrar y conectar Neo4j con otras plataformas y lenguajes, se han desarrollado distintos conectores para integrar esta plataforma con tecnolog√≠as como Apache Spark, Apache Kafka, BI... 

- **Herramientas para desarrolladores**: Neo4j dispone de versiones para escritorio y web as√≠ como un sandbox en el que se proporciona un entorno controlado e integrado de los servicios Neo4j para desarrolladores.

- **Neo4j Aura**: Neo4j ofrece un servicio de computaci√≥n en la nube para la creaci√≥n de grafos y ejecuci√≥n de algoritmos y modelos sobre ellos. Se trata de Neo4j Aura, el cual se encuentra disponible de forma gratuita y est√° integrado con Google Cloud Platform.

Las caracter√≠sticas anteriormente mencionadas y la cantidad de herramientas disponibles que Neo4j pone a disposici√≥n de los desarrolladores han sido los motivos principales para optar por esta plataforma para ilustrar el trabajo con bases de datos orientadas a grafos.

_[ver presentaci√≥n BDA4.2](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_02.pdf)_

### 2.2 Importaci√≥n de datos. Creaci√≥n de grafos

_[ver presentaci√≥n BDA4.3](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_03.pdf)_

Para comenzar a trabajar con Neo4j, es necesario **crear un grafo o bien partir de un grafo ya existente** que se importa dentro del √°rea de trabajo de Neo4j. A continuaci√≥n, se muestra c√≥mo implementar ambos procedimientos.

**Importaci√≥n de un grafo**

Sea el caso en el que se pretende importar un grafo a partir de un fichero .csv. En el ejemplo que se muestra a continuaci√≥n, se dispone de dos ficheros .csv que contienen, respectivamente, **la definici√≥n de los nodos y de las aristas del grafo que se pretende importar**. As√≠ pues, el listado 4.1 muestra los nodos del grafo. Para cada nodo se almacena su identificador, latitud, longitud y poblaci√≥n

```
id,latitude,longitude,population
"Amsterdam",52.379189,4.899431,821752
"Utrecht",52.092876,5.104480,334176
"Den Haag",52.078663,4.288788,514861
"Immingham",53.61239,-0.22219,9642
"Doncaster",53.52285,-1.13116,302400
"Hoek van Holland",51.9775,4.13333,9382
"Felixstowe",51.96375,1.3511,23689
"Ipswich",52.05917,1.15545,133384
"Colchester",51.88921,0.90421,104390
"London",51.509865,-0.118092,8787892
"Rotterdam",51.9225,4.47917,623652
"Gouda",52.01667,4.70833,70939
```
_Listado 4.1: Definici√≥n de nodos de un grafo_

Por su parte, el listado 4.2 especifica cada uno de los enlaces entre los nodos del grafo. Para cada uno de los enlaces se identifica el origen y destino del enlace, el tipo de relaci√≥n que se define entre ellos y el coste. 

```
src,dst,relationship,cost
"Amsterdam","Utrecht","EROAD",46
"Amsterdam","Den Haag","EROAD",59
"Den Haag","Rotterdam","EROAD",26
"Amsterdam","Immingham","EROAD",369
"Immingham","Doncaster","EROAD",74
"Doncaster","London","EROAD",277
"Hoek van Holland","Den Haag","EROAD",27
"Felixstowe","Hoek van Holland","EROAD",207
"Ipswich","Felixstowe","EROAD",22
"Colchester","Ipswich","EROAD",32
"London","Colchester","EROAD",106
"Gouda","Rotterdam","EROAD",25
"Gouda","Utrecht","EROAD",35
"Den Haag","Gouda","EROAD",32
"Hoek van Holland","Rotterdam","EROAD",33
```
_Listado 4.2: Definici√≥n de aristas de un grafo_

Dados estos dos ficheros, es posible **importar los nodos del grafo** seg√∫n se indica en el listado 4.3.

```
 // Importamos los nodos
WITH "https://github.com/neo4j-graph-analytics/book/raw/master/data/transport-nodes.csv" AS uri
LOAD CSV WITH HEADERS FROM uri AS row
MERGE (place:Place {id:row.id})
SET place.latitude = toFloat(row.latitude),
place.longitude = toFloat(row.latitude),
place.population = toInteger(row.population)
```
_Listado 4.3: Importaci√≥n de nodos_

As√≠, en el listado anterior se define como identificador de recurso la direcci√≥n donde se encuentra el archivo que se pretende cargar, el cual es cargado a trav√©s de la instrucci√≥n **LOAD CSV**. La instrucci√≥n **MERGE** permite definir cada fila como nodo de un grafo de tipo lugar (place) y a√±adir a cada nodo la propiedad id cuyo valor ser√° el identificador de la fila en la que se encuentra cada nodo dentro del fichero csv. Finalmente, los valores de latitud y longitud se convierten en valores de tipo real y la poblaci√≥n en un valor de tipo entero.

An√°logamente a la importaci√≥n de nodos, el listado 4.4 muestra el c√≥digo necesario para la **importaci√≥n del fichero que contiene los enlaces** entre los nodos del grafo.

```
//Importamos las relaciones
WITH "https://github.com/neo4j-graph-analytics/book/raw/master/data/transport-relationships.csv" AS uri
LOAD CSV WITH HEADERS FROM uri AS row
MATCH (origin:Place {id: row.src})
MATCH (destination:Place {id: row.dst})
MERGE (origin)-[:EROAD {distance: toInteger(row.cost)}]->(destination)
```
_Listado 4.4: Importaci√≥n de aristas_

La diferencia principal de este listado con respecto al anterior, es que en este se necesita identificar mediante la instrucci√≥n **MATCH** los nodos de origen y destino de cada uno de los enlaces, lo cual es posible mediante los atributos ‚Äúsrc‚Äù y ‚Äúdst‚Äù incluidos en el archivo .csv. Finalmente, se utiliza la sentencia **MERGE** para crear una relaci√≥n entre ‚Äúorigin‚Äù y ‚Äúdestination‚Äù de tipo **EROAD** que contiene la propiedad ‚Äúdistance‚Äù cuyo valor es el coste que aparec√≠a en el fichero .csv.

**Creaci√≥n de un grafo**

Otra alternativa es la **creaci√≥n de un grafo desde cero**, especificando para ello sus nodos y enlaces. La [figura4.2] muestra un ejemplo de un **grafo de coocurrencia de hashtags** en el que cada uno de los nodos es un hashtag y los enlaces entre ellos se producen cuando dos hashtags aparecieron en un mismo tweet. Sobre cada enlace, se puede apreciar un valor que indica en cu√°ntos tweets concurrieron los hashtags conectados por medio de dicha arista.

![figura4.2]

_Figura 4.2: Grafo de co-ocurrencia de hashtags_

Para representar este grafo en Neo4j, se puede escribir el siguiente fragmento de c√≥digo que muestra el listado 4.5.

```
CREATE
(JS:Hashtag {name: 'JoaquinSabina'}),
(RS:Hashtag {name: 'Rusia2018'}),
(AG:Hashtag {name: 'Argentina'}),
(FD:Hashtag {name: 'Feliz Domingo'}),
(MS:Hashtag {name: 'Messi'}),

(JS)-[:COOC {ntweet: 52}]->(FD),
(FD)-[:COOC {ntweet: 52}]->(JS),
(RS)-[:COOC {ntweet: 183}]->(FD),
(FD)-[:COOC {ntweet: 183}]->(RS),
(RS)-[:COOC {ntweet: 73}]->(AG),
(AG)-[:COOC {ntweet: 73}]->(RS),
(AG)-[:COOC {ntweet: 112}]->(MS),
(MS)-[:COOC {ntweet: 112}]->(AG),
(FD)-[:COOC {ntweet: 81}]->(MS),
(MS)-[:COOC {ntweet: 81}]->(FD)
```
_Listado 4.5: Creaci√≥n grafo de co-ocurrencia de hashtags_

Tal y como se puede ver, en primer lugar se definen los nodos y sus propiedades y a continuaci√≥n los enlaces y sus propiedades. **Todo grafo en Neo4j tiene aristas dirigidas, si bien es cierto que cuando se ejecutan las consultas puede no tenerse en cuenta la direcci√≥n de las aristas**. Por este motivo, se han especificado las aristas en ambas direcciones. En caso de querer crear una arista que sea interpretada como no dirigida, se debe crear sin el operador flecha que determina el sentido de la arista. Esto es posible hacerlo solo fuera de la instrucci√≥n **CREATE**. El listado 4.6 muestra c√≥mo crear la primera relaci√≥n del grafo solo que de forma no dirigida.

```
MATCH(JS:Hashtag{name:'JoaquinSabina'})
MATCH(FD:Hashtag{name:'Feliz Domingo'})
MERGE (JS)-[:COOC{ntweet:52}]-(FD)
```
_Listado 4.6: Creaci√≥n de una arista no dirigida_

Para **mostrar el grafo**, es posible utilizar el siguiente comando. La figura 4.3 muestra c√≥mo quedar√≠a el grafo creado en el listado 4.5.

    MATCH(n) return (n)

Para eliminar un nodo creado, es posible utilizar el comando:

    MATCH(JS:Hashtag{name:'JoaquinSabina'}) delete (JS)

Sin embargo, **un nodo solo puede ser eliminado si se eliminan las relaciones** que contiene. Para ello, es posible utilizar el comando:

![figura4.3]

_Figura 4.3: Visualizaci√≥n del grafo de co-ocurrencia de hashtags_

    MATCH(JS:Hashtag{name:'JoaquinSabina'}) detach delete (JS)

Mientras que, para eliminar todos los nodos y aristas, es posible escribir:

    MATCH(n) detach delete(n)

### 2.3 Recorridos sobre grafos

Los recorridos sobre grafos son una utilidad imprescindible en el trabajo con esta estructura de datos. Del mismo modo, los recorridos tambi√©n son una herramienta fundamental cuando se trabaja con bases de datos orientadas a grafos.

**Un recorrido sobre grafos es un procedimiento sistem√°tico que permite explorar un grafo examinando todos sus v√©rtices y aristas, comenzando desde un v√©rtice inicial**. A la hora de recorrer un grafo, existen dos tipos de recorridos: el **recorrido en anchura** (denotado BFS, por sus siglas en ingl√©s) y el **recorrido en profundidad** (denotado DFS, tambi√©n por sus siglas en ingl√©s).

_[ver presentaci√≥n BDA4.4](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_04.pdf)_

**Recorrido en anchura ([BFS](https://neo4j.com/docs/graph-data-science/current/algorithms/bfs/))**

El recorrido en anchura, tambi√©n conocido por sus siglas en ingl√©s _Breadth First Search (BFS)_ es un procedimiento que permite recorrer un grafo, explorando todos sus nodos y aristas.

El recorrido en anchura parte de un nodo inicial. A continuaci√≥n, el m√©todo comienza a explorar todos los nodos hijo del nodo inicial seleccionado que se encuentran a un salto. Una vez visitados, comienzan a explorarse los nodos hijo del siguiente nivel y as√≠ sucesivamente hasta haber recorrido el grafo completo. Este recorrido es muy utilizado cuando se busca un camino con el n√∫mero m√≠nimo de aristas entre dos v√©rtices dados o cuando se busca un ciclo simple, es decir, una secuencia de nodos y aristas que se pueden describir circularmente en el que todos los nodos y aristas son distintos.


Por ejemplo, sea el grafo de la [figura4.4] que se corresponde con el grafo que se import√≥ anteriormente. Es posible recorrerlo utilizando BFS escribiendo el siguiente c√≥digo Neo4j que aparece en el listado 4.7. 

![figura4.4]

_Figura 4.4: Visualizaci√≥n del grafo de ciudades_

```
// Creamos el grafo. 
CALL gds.graph.project(
    'myGraph_BFR',
    'Place',
    'EROAD'
)
```

```
//Realizamos la b√∫squeda en anchura
MATCH (a:Place{id:'Doncaster'})
WITH id(a) AS startNode
CALL gds.bfs.stream('myGraph_BFR', {sourceNode: startNode})
YIELD path
UNWIND [ n in nodes(path) | n.id ] AS tags
RETURN tags
```
_Listado 4.7: Recorrido en anchura_

En el fragmento anterior, se define un grafo llamado _myGraph\_BFR_ el cual est√° formado por nodos de tipo Place y cuyas relaciones son del tipo **EROAD** y tienen una propiedad, denominada _distance_. A continuaci√≥n, se define el nodo inicial con las sentencias **MATCH** y **WITH**. Despu√©s, se llama al m√©todo que realiza el recorrido BFS sobre el grafo anterior partiendo desde el nodo inicial para producir un camino (_path_). Finalmente, se devuelven todos los nodos que forman parte del camino, mostrando los identificadores de cada uno de ellos. El resultado del recorrido es: Doncaster, London, Colchester, Ipswich, Felixstowe, Hoek van Holand, Den Haag, Rotterdam, Gouda, Utrecht.

**Recorrido en profundidad ([DFS](https://neo4j.com/docs/graph-data-science/current/algorithms/dfs/))**

El recorrido en profundidad, tambi√©n conocido por sus siglas en ingl√©s _Depth First Search (DFS)_ es un procedimiento alternativo al recorrido en anchura para explorar un grafo, recorriendo todos sus nodos y aristas.

El procedimiento para recorrer un grafo mediante el m√©todo DFS es el siguiente: en primer lugar, se parte de un nodo inicial. A partir de √©l, se empiezan a recorrer todos sus hijos hasta el √∫ltimo nivel. Una vez llegados al √∫ltimo nivel del grafo, se vuelve hacia atr√°s recorriendo todos los hijos de los nodos de niveles anteriores. 

En Neo4j, el procedimiento para recorrer un grafo utilizando el recorrido BFS se muestra en el listado de c√≥digo 4.8. El procedimiento, como se puede comprobar, es pr√°cticamente id√©ntico al del listado 4.7 solo que cambiando el m√©todo de recorrido. El resultado obtenido es: Doncaster, London, Colchester, Ipswich, Felixstowe, Hoek van Holland, Rotterdam, Den Haag, Gouda, Utrecht.

```
// Creamos el grafo. 
CALL gds.graph.project(
    'myGraph_DFS',
    'Place',
    'EROAD'
)


//Realizamos la b√∫squeda en Profundidad
MATCH (a:Place{id:'Doncaster'})
WITH id(a) AS startNode
CALL gds.dfs.stream('myGraph_BFR', {sourceNode: startNode})
YIELD path
UNWIND [ n in nodes(path) | n.id ] AS tags
RETURN tags
```
_Listado 4.8: Recorrido en profundidad_

### 2.4 Caminos m√≠nimos

El c√°lculo y la obtenci√≥n de caminos m√≠nimos dentro de un grafo es uno de los problemas cl√°sicos en teor√≠a de grafos con multitud de aplicaciones en el mundo real. **Un camino dentro de un grafo es un conjunto de nodos y aristas que conectan un par de nodos del grafo**. En el caso de los **grafos no pesados**, es decir, aquellos cuyas aristas no tienen coste, el c√°lculo del camino m√≠nimo se obtiene **minimizando el n√∫mero de saltos** entre el nodo origen y el nodo destino. En el caso de los **grafos pesados**, aquellos cuyas aristas tienen coste, el c√°lculo del camino m√≠nimo se obtiene a trav√©s del camino en el que **la suma de los costes de sus aristas es m√≠nima**.

_[ver presentaci√≥n BDA4.5](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_05.pdf)_

Junto con los recorridos de grafos, los m√©todos de obtenci√≥n de caminos m√≠nimos son muy √∫tiles cuando se trabaja en **entornos din√°micos**, donde aparecen y desaparecen continuamente aristas del grafo o sus costes cambian din√°micamente. El c√°lculo de caminos m√≠nimos tambi√©n es muy √∫til en aplicaciones que deben dar **respuestas en tiempo real**. Algunos ejemplos claros pueden ser encontrar rutas entre dos localidades, como ocurre en Google Maps u obtener los grados de separaci√≥n entre una empresa y un empleado potencial al que se quiere contratar, como puede ocurrir en LinkedIn.

- **Algoritmo de Dijkstra**

El **algoritmo de Dijkstra** es uno de los algoritmos m√°s utilizados en teor√≠a de grafos para la obtenci√≥n de caminos m√≠nimos. Dado el grafo de ciudades con el que se est√° trabajando a lo largo de este cap√≠tulo, el listado 4.9 permite obtener **los caminos m√≠nimos entre un nodo del grafo y todos los dem√°s nodos alcanzables([Single-Source Shortest Path](https://neo4j.com/docs/graph-data-science/current/algorithms/dijkstra-single-source/))**.

```
// Creamos el grafo. 
CALL gds.graph.project(
    'myGraph_Dijkstra',
    'Place',
    'EROAD',
    {
        relationshipProperties: 'distance'
    }
)


MATCH (source:Place {id: 'Amsterdam'})
CALL gds.allShortestPaths.dijkstra.stream('myGraph_Dijkstra', {
    sourceNode: source,
    relationshipWeightProperty: 'distance'
})
YIELD index, sourceNode, targetNode, totalCost, nodeIds, costs, path
RETURN
    index,
    gds.util.asNode(sourceNode).id AS sourceNodeName,
    gds.util.asNode(targetNode).id AS targetNodeName,
    totalCost,
    [nodeId IN nodeIds | gds.util.asNode(nodeId).id] AS nodeNames,
    costs,
    nodes(path) as path
ORDER BY index
```
_Listado 4.9: Ejecuci√≥n del algoritmo de Dijkstra sobre el nodo Amsterdam_

En el listado anterior, se utiliza la sentencia **MATCH** para definir el nodo de origen y, posteriormente, se guarda el grafo incluyendo el nodo de origen y la propiedad de distancia que es la utilizada para obtener el camino m√≠nimo. A continuaci√≥n, se producir√° un conjunto de caminos en el que se mostrar√° informaci√≥n completa del nodo de origen, destino, coste total y camino recorrido as√≠ como los distintos costes parciales. En caso de querer obtener el **camino m√≠nimo entre un par de nodos concretos([Single Source-Target Shortest Path](https://neo4j.com/docs/graph-data-science/current/algorithms/dijkstra-source-target/))**, es posible utilizar el c√≥digo mostrado en el listado 4.10 an√°logo al listado 4.9.

```
MATCH (source:Place {id: 'Amsterdam'}), (target:Place {id: 'London'})
CALL gds.shortestPath.dijkstra.stream('myGraph_Dijkstra', {
    sourceNode: source,
    targetNode: target,
    relationshipWeightProperty: 'distance'
})
YIELD index, sourceNode, targetNode, totalCost, nodeIds, costs, path
RETURN
    index,
    gds.util.asNode(sourceNode).id AS sourceNodeName,
    gds.util.asNode(targetNode).id AS targetNodeName,
    totalCost,
    [nodeId IN nodeIds | gds.util.asNode(nodeId).id] AS nodeNames,
    costs,
    nodes(path) as path
ORDER BY index
```
_Listado 4.10: Ejecuci√≥n del algoritmo de Dijkstra sobre el par de nodos Amsterdam-London_

El camino m√≠nimo entre Amsterdam y Londres es el formado por: Amsterdam Immingham, Doncaster, London. Este camino tiene un coste total de 720.

- **[Algoritmo A\*](https://neo4j.com/docs/graph-data-science/current/algorithms/astar/)**

El **algoritmo A\* es una t√©cnica de b√∫squeda informada** que permite, al igual que el algoritmo de Dijkstra, **calcular el camino m√≠nimo entre dos nodos dados cualesquiera**. Al contrario que el algoritmo de Dijkstra, cuando se va a seleccionar el siguiente nodo del camino, la t√©cnica A* no solo se tiene en cuanta la distancia ya calculada, sino que **este algoritmo combina la distancia calculada con el resultado de una funci√≥n heur√≠stica**. Esta funci√≥n toma un nodo como entrada y devuelve un valor que corresponde con el coste de llegar al nodo objetivo desde ese nodo. Esta t√©cnica, que se conoce tambi√©n como de **b√∫squeda informada**, contin√∫a el recorrido del grafo en cada iteraci√≥n desde el nodo con el menor coste combinado entre la distancia ya calculada y la funci√≥n heur√≠stica. El listado 4.11 muestra c√≥mo obtener el camino m√≠nimo mediante el m√©todo A* entre Amsterdam y Londres.

```
// Creamos el grafo. 
CALL gds.graph.project(
    'myGraph_A',
    'Place',
    'EROAD',
    {
        nodeProperties: ['latitude', 'longitude', 'population'],
        relationshipProperties: 'distance'
    }
)


MATCH (source:Place {id: 'Amsterdam'}), (target:Place {id: 'London'})
CALL gds.shortestPath.astar.stream('myGraph_A', {
    sourceNode: source,
    targetNode: target,
    latitudeProperty: 'latitude',
    longitudeProperty: 'longitude',
    relationshipWeightProperty: 'distance'
})
YIELD index, sourceNode, targetNode, totalCost, nodeIds, costs, path
RETURN
    index,
    gds.util.asNode(sourceNode).id AS sourceNodeName,
    gds.util.asNode(targetNode).id AS targetNodeName,
    totalCost,
    [nodeId IN nodeIds | gds.util.asNode(nodeId).id] AS nodeNames,
    costs,
    nodes(path) as path
ORDER BY index
```
_Listado 4.11: Ejecuci√≥n del algoritmo A* sobre el par de nodos Amsterdam-London_

Como se puede ver, este fragmento de c√≥digo es similar a los vistos anteriormente, solo que a la hora de guardar el grafo, se almacenan los propiedades de latitud y longitud, necesarias en Neo4j para ejecutar el algoritmo A*.

### 2.5 Medidas de centralidad

**La centralidad se define como la relevancia de un nodo dentro de un grafo**. Por tanto, el estudio de medidas de centralidad permite **identificar nodos relevantes dentro de un grafo**. Esta identificaci√≥n permitir√° entender el comportamiento de la red, qu√© nodos permiten viralizar con mayor rapidez el contenido de la red, desde qu√© nodos la informaci√≥n est√° m√°s accesible, etc. El estudio de medidas de centralidad tiene multitud de aplicaciones, aunque son especialmente notables las relacionadas con los √°mbitos de la **publicidad y el marketing**, donde el estudio de estas m√©tricas permite identificar actores relevantes dentro de una red a los que se puede contratar para anunciar un producto o identificar sobre qu√© actores enviar informaci√≥n para alcanzar a m√°s usuarios.

Respecto a las medidas de centralidad, no existe una √∫nica medida sino que, en funci√≥n de los datos y del prop√≥sito que se pretende alcanzar, se utilizan unas m√©tricas u otras. a continuaci√≥n, se van a definir tres medidas de centralidad con las que se realizar√°n ejemplos en Neo4j: **centralidad de grado, cercan√≠a e intermediaci√≥n**. 

_[ver presentaci√≥n BDA4.6](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_06.pdf)_

Para trabajar con medidas de centralidad, se va a importar un grafo social que servir√° de ejemplo para el c√°lculo de estas m√©tricas. El listado 4.12 muestra el c√≥digo necesario para su creaci√≥n. Por otra parte, la [figura4.5] muestra una visualizaci√≥n del grafo importado.

```
// Importamos los nodos
WITH "https://github.com/neo4j-graph-analytics/book/raw/master/data/" AS base
WITH base + "social-nodes.csv" AS uri
LOAD CSV WITH HEADERS FROM uri AS row
MERGE (:User {id: row.id})

//Importamos las relaciones
WITH "https://github.com/neo4j-graph-analytics/book/raw/master/data/" AS base
WITH base + "social-relationships.csv" AS uri
LOAD CSV WITH HEADERS FROM uri AS row
MATCH (source:User {id: row.src})
MATCH (destination:User {id: row.dst})
MERGE (source)-[:FOLLOWS]->(destination)
```
_Listado 4.12: Importaci√≥n de un grafo social_

![figura4.5]

Figura 4.5: Visualizaci√≥n de un grafo social

**[Centralidad de grado (Degree Centrality)](https://neo4j.com/docs/graph-data-science/current/algorithms/degree-centrality/)**

**El grado de un nodo en un grafo se define como el n√∫mero de aristas o conexiones que entran o salen de un nodo**. As√≠ pues, en un nodo de un grafo se distinguen dos grados: el **grado de entrada**, correspondiente al conjunto de conexiones que entran a un nodo y el **grado de salida**, que se corresponde con el conjunto de aristas que salen de un nodo. El primero indica la **prominencia** de un nodo dentro de la red, mientras que el segundo la **influencia** del nodo dentro del grafo. Esta medida de centralidad es muy utilizada para identificar actores prominentes o influyentes o para identificar conductas fraudulentas, caracterizadas en ocasiones por una actividad anormal. Para calcular la centralidad de grado en este grafo, se almacenar√° en primer lugar el grafo creado y, a continuaci√≥n, se ejecutar√° el m√©todo que calcula la centralidad de grado seg√∫n el c√≥digo mostrado en el listado 4.13. El nodo con mayor grado es Doug, que tiene un grado de 5, seguido de Alice que tiene un grado de 3.

```
CALL gds.graph.project(
'myGraph_centralidad_grado',
'User',
{
    FOLLOWS: {
    orientation: 'REVERSE'
    }
}
)
```

Como no tenemos ninguna propiedad/coste/puntuci√≥n en la relaci√≥n, no hay que indicarla. Si la quisieramos tener en cuenta habr√≠a que a√±adirla dentro de follow de la siguiente forma:

```
CALL gds.graph.project(
'myGraph_centralidad_grado',
'User',
{
    FOLLOWS: {
    orientation: 'REVERSE',
    properties: ['score']
    }
}
)
```

La centralidad de grado de este grafo ser√≠a

```
CALL gds.degree.stream('myGraph_centralidad_grado')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).id AS id, score AS followers
ORDER BY followers DESC, id DESC
```
_Listado 4.13: C√°lculo de la centralidad de grado_

**[Cercan√≠a (Closeness Centrality)](https://neo4j.com/docs/graph-data-science/current/algorithms/closeness-centrality/)**
_Beta a 9-11-2022_

**La cercan√≠a es una medida de centralidad que determina qu√© nodos del grafo expanden r√°pida y eficientemente la informaci√≥n a trav√©s del grafo**. Para calcular esta medida, se obtiene **la suma del inverso de las distancias de un nodo al resto**. De esta forma, dado un nodo u la cercan√≠a del mismo se calcula por medio de la [ecuaci√≥n4.1].

![ecuaci√≥n4.1]

Donde _n_ es el n√∫mero de nodos del grafo y _d(u, v)_ es la distancia del camino m√≠nimo entre _u_ y _v_. **La cercan√≠a es una m√©trica muy utilizada para estimar tiempos de llegada en redes log√≠sticas, para descubrir actores en posiciones privilegiadas en redes sociales o para estudiar la prominencia de palabras en un documento en el campo de la miner√≠a de textos**. Para obtener la cercan√≠a en el grafo social de ejemplo, se puede utilizar el fragmento de c√≥digo mostrado en el listado 4.14. En el grafo de la imagen, Doug y David tienen una cercan√≠a de 1.

```
CALL gds.graph.project(
    'myGraph_cercania',
    'User',
    'FOLLOWS'
)

CALL gds.beta.closeness.stream('myGraph_cercania')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).id AS id, score
ORDER BY score DESC
```
_Listado 4.14: C√°lculo de la cercan√≠a_

**[Indeterminaci√≥n (Betweenness Centrality)](https://neo4j.com/docs/graph-data-science/current/algorithms/betweenness-centrality/)**

**La intermediaci√≥n es una medida de centralidad que permite detectar la influencia que tiene un nodo o actor del grafo en el flujo de informaci√≥n o de recursos de la red**. El c√°lculo de la intermediaci√≥n permite identificar a nodos que hacen de puentes entre distintas porciones del grafo. Esta medida de centralidad **es muy utilizada para la identificaci√≥n de influencers y el estudio de la viralizaci√≥n de mensajes en redes sociales.** Intuitivamente, la intermediaci√≥n de un nodo **ser√° mayor en tanto en cuanto dicho nodo aparezca en los caminos m√≠nimos de cualquier otro par de nodos**. M√°s formalmente, la intermediaci√≥n puede calcularse seg√∫n la [ecuaci√≥n4.2].

![ecuaci√≥n4.2]

Donde _u_ es el nodo del cual se calcula la intermediaci√≥n _(B),s_ y _t_ son nodos del grafo, _p(u)_ es el n√∫mero de caminos m√≠nimos entre _s_ y _t_ que pasan por _u_ y _p_ es el n√∫mero de caminos m√≠nimos entre s y t. Para el c√°lculo de la intermediaci√≥n, se puede utilizar el fragmento de c√≥digo mostrado en el listado 4.15. Al ejecutar este listado, se obtiene que Alice tiene la mayor intermediaci√≥n, teniendo un valor de 10.

```
CALL gds.graph.project(
    'myGraph_intermediacion',
    'User',
    'FOLLOWS'
)
```

Como no tenemos ninguna propiedad/coste/puntuci√≥n en la relaci√≥n, no hay que indicarla. Si la quisieramos tener en cuenta habr√≠a que a√±adirla dentro de follow de la siguiente forma:

```
CALL gds.graph.project(
    'myGraph_intermediacion',
    'User',
    {
        FOLLOWS:
        {
            properties: 'weight'
        }
    }
)
```

El c√°lculo de la indeterminaci√≥n ser√≠a

```
CALL gds.betweenness.stream('myGraph_intermediacion')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).id AS id, score
ORDER BY score DESC
```
_Listado 4.15: C√°lculo de la intermediaci√≥n_

### 2.6 Detecci√≥n de comunidades

A la hora de trabajar con grafos reales, que presentan una gran cantidad de nodos y enlaces, muchas veces se pretende **identificar comunidades** dentro del grafo para aplicar algoritmos sobre ellas. **Una comunidad es, por tanto, un conjunto de nodos que presentan m√°s relaciones entre s√≠ que con el resto de nodos fuera de la comunidad**. La detecci√≥n e identificaci√≥n de comunidades permite **identificar comportamientos emergentes y de reba√±o dentro de una red**. De esta forma, es posible detectar y predecir h√°bitos de los usuarios. A continuaci√≥n, se van a aplicar tres m√©todos de detecci√≥n de comunidades sobre el grafo social de ejemplo que se ha utilizado en secciones anteriores.

_[ver presentaci√≥n BDA4.7](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_07.pdf)_

**[Conteo de tri√°ngulos(Triangle Count)](https://neo4j.com/docs/graph-data-science/current/algorithms/triangle-count/)**

**Un tri√°ngulo es un conjunto de tres nodos que tienen relaciones entre s√≠**. El conteo de tri√°ngulos para un nodo dado, permite estudiar o inspeccionar de forma global un grafo y, aplicado sobre componentes conexas, permite inspeccionar regiones de un grafo. Para aplicar este m√©todo, es necesario almacenar el grafo teniendo en cuenta que las aristas deben almacenarse como **UNDIRECTED**. El listado 4.16 muestra el fragmento de c√≥digo necesario para aplicar este m√©todo sobre el grafo social de ejemplo. La ejecuci√≥n de este m√©todo da como resultado que Alice y Doug son aquellos que pertenecen a m√°s tri√°ngulos, un total de 5.

```
CALL gds.graph.project(
    'myGraph_conteo_triangulo',
    'User',
    {
        FOLLOWS: {
        orientation: 'UNDIRECTED'
        }
    }
)

CALL gds.triangleCount.stream('myGraph_conteo_triangulo')
YIELD nodeId, triangleCount
RETURN gds.util.asNode(nodeId).id AS id, triangleCount
ORDER BY triangleCount DESC
```
_Listado 4.16: Ejecuci√≥n del conteo de tri√°ngulos_

**[Coeficiente local de clustering (Local Clustering Coefficient)](https://neo4j.com/docs/graph-data-science/current/algorithms/local-clustering-coefficient/)**

Este coeficiente **proporciona una medida cuantitativa del grado de agrupaci√≥n de un nodo**. Para el c√°lculo del coeficiente local de clustering se utiliza el conteo de tri√°ngulos, de forma que se compara el grado de agrupaci√≥n de un nodo con el grado m√°ximo de agrupaci√≥n que podr√≠a tener. As√≠ pues, el coeficiente local de clustering se puede calcular mediante la [ecuaci√≥n4.3].

![ecuaci√≥n4.3]

Donde _u_ es un nodo, _R(u)_ es el n√∫mero de relaciones que a trav√©s de los vecinos de _u_ (lo cual puede medirse con el n√∫mero de tri√°ngulos que pasan por _u_ y _k(u)_ es el grado de _u_. El c√°lculo del coeficiente local de clustering puede realizarse por medio del listado 4.17. Los actores Bridget, Charles, Mark y Michael tienen el mayor coeficiente local de clustering, que es 1. 

```
CALL gds.graph.project(
    'myGraph_LCC',
    'User',
    {
        FOLLOWS: {
        orientation: 'UNDIRECTED'
        }
    }
)

CALL gds.localClusteringCoefficient.stream('myGraph_LCC')
YIELD nodeId, localClusteringCoefficient
RETURN gds.util.asNode(nodeId).id AS id, localClusteringCoefficient
ORDER BY localClusteringCoefficient DESC
```
_Listado 4.17: C√°lculo del coeficiente local de clustering_

Por √∫ltimo, **el coeficiente global de clustering se calcula como la suma normalizada de los coeficientes de clustering locales**. As√≠, estos coeficientes nos permiten encontrar medidas cuantitativas para detectar comunidades, pudiendo especificar incluso un umbral para establecer la comunidad (por ejemplo, especificar que los nodos han de estar conectados en un 40 %).

**[Componentes fuertemente conexas (Strongly Connected Components)](https://neo4j.com/docs/graph-data-science/current/algorithms/strongly-connected-components/)**
_alpha a 09-11-2022_

**En un grafo dirigido, una componente fuertemente conexa es aquel grupo de nodos en el que cualquier nodo puede ser alcanzado por cualquier otro en ambas direcciones**. El estudio de las componentes fuertemente conexas en un grafo permite **estudiar la conectividad de la red**. Para realizar el c√°lculo de las componentes conexas, el cual se realiza en un tiempo de procesamiento proporcional al n√∫mero de nodos, es posible utilizar el listado 4.18. El resultado permite comprobar que hay un total de cuatro componentes fuertemente conexas en el grafo social.

```
CALL gds.graph.project(
    'myGraph_SCC',
    'User',
    'FOLLOWS'
)

CALL gds.alpha.scc.stream('myGraph_SCC', {})
YIELD nodeId, componentId
RETURN gds.util.asNode(nodeId).id AS Name, componentId AS Component
ORDER BY Component DESC
```
_Listado 4.18: Obtenci√≥n de componentes fuertemente conexas_

### 2.7 Predicci√≥n de enlaces

Los grafos son estructuras de datos que representan sistemas din√°micos, que evolucionan a lo largo del tiempo. Por este motivo, es muy com√∫n que en un grafo cualquiera aparezcan y desaparezcan nuevos nodos y conexiones entre dichos nodos. **Los m√©todos de predicci√≥n de enlaces permiten predecir qu√© enlaces se formar√°n pr√≥ximamente entre los nodos del grafo, permitiendo adelantarse a los acontecimientos y prevenir eventualidades**. Como norma general, los m√©todos de predicci√≥n de enlaces **se basan en medidas de cercan√≠a y de centralidad** asumiendo que los nuevos enlaces se producir√°n, mayoritariamente, sobre/desde los nodos m√°s relevantes. A continuaci√≥n, se detalla el funcionamiento de tres m√©todos com√∫nmente utilizados en predicci√≥n de enlaces. 

_[ver presentaci√≥n BDA4.8](https://moodle.iesgrancapitan.org/pluginfile.php/58314/mod_folder/content/0/BDA_UD4_08.pdf)_

**[Vecinos comunes (Common Neighbors)](https://neo4j.com/docs/graph-data-science/current/alpha-algorithms/common-neighbors/)**
_alpha a 09-11-2022_

El m√©todo de vecinos comunes se basa en la idea gen√©rica de que dos actores de la red que tienen una relaci√≥n con un usuario com√∫n tendr√°n m√°s posibilidad de conectarse entre s√≠ que quienes no. Formalmente, dados dos nodos, la posibilidad de que se produzca un enlace entre los nodos _x_ e _y_ viene dada por la [ecuaci√≥n4.4].

![ecuaci√≥n4.4]

Donde _N(x)_ es el conjunto de nodos adyacentes a _x_ y _N(y)_ es el conjunto de nodos adyacentes a _y_. Cuanto mayor es el valor de _CN_ calculado, existe mayor posibilidad de que se produzca un nuevo enlace entre _x_ e _y_. En el grafo social de ejemplo, el c√°lculo de vecinos comunes para Charles y Bridget se puede realizar a trav√©s del listado 4.19. El resultado de este c√°lculo es 2.

```
//No funciona ni en la documentaci√≥n oficial (09-11-2022)
MATCH (x:User{id:'Charles'})
MATCH (y:User{id:'Bridget'})
RETURN gds.alpha.linkprediction.commonNeighbors(x,y) AS score
```
_Listado 4.19: Obtenci√≥n del valor de vecinos comunes para Charles y Bridget_

**[Adhesi√≥n preferencial (Preferential Attachment)](https://neo4j.com/docs/graph-data-science/current/alpha-algorithms/preferential-attachment/)**
_alpha a 09-11-2022_

Este m√©todo se basa en la idea general de que cuanto m√°s conectado est√° un nodo, es m√°s probable que reciba nuevos enlaces. Formalmente, esta idea se puede expresar por medio de la [ecuaci√≥n4.5]

![ecuaci√≥n4.5]

En el grafo social de ejemplo, el c√°lculo de adhesi√≥n preferencial para Charles y Bridget se puede realizar a trav√©s del listado 4.20. El resultado de este c√°lculo es 10.

```
MATCH (x:User{id:'Charles'})
MATCH (y:User{id:'Bridget'})
RETURN gds.alpha.linkprediction.preferentialAttachment(x, y) AS score
```
_Listado 4.20: Obtenci√≥n del valor de adhesi√≥n preferencial para Charles y Bridget_

**[Asignaci√≥n de recursos (Resource Allocation)](https://neo4j.com/docs/graph-data-science/current/alpha-algorithms/resource-allocation/)**
_alpha a 09-11-2022_

Se trata de una m√©trica compleja que eval√∫a la cercan√≠a de un par de nodos para determinar la posibilidad de que, entre ellos, se produzca un nuevo enlace. Para ello, se utiliza la expresi√≥n dada en la [ecuaci√≥n4.6].

![ecuaci√≥n4.6]

En el grafo social de ejemplo, el c√°lculo de la asignaci√≥n de recursos para Charles y Bridget se puede realizar a trav√©s del listado 4.21. El resultado de este c√°lculo es 0.309.

```
//No funciona ni en la documentaci√≥n oficial (09-11-2022)
MATCH (x:User{id:'Charles'})
MATCH (y:User{id:'Bridget'})
RETURN gds.alpha.linkprediction.resourceAllocation(x, y) AS score
```
_Listado 4.21: Obtenci√≥n del valor de asignaci√≥n de recursos para Charles y Bridget_

### 2.8 Machine Learning

Existe tambi√©n la posibilidad de trabajar con **Machine learning** en los sistema de Big Data orientados a grafos como Neo4j

Toda la documentaci√≥n de los mismos se encuentra tambi√©n en la [documentaci√≥n oficial](https://neo4j.com/docs/graph-data-science/current/machine-learning/machine-learning/). Esta funcionalidad no forma parte de los Resultados de Aprendizaje de este m√≥dulo, ya que existe un m√≥dulo especif√≠co para este tema. A√∫n as√≠, podr√≠a ser interesante investigarlo e intentar integrarlo como una pr√°ctica opcional üòâ

[figura4.1]: images/Figura4.1_Ejemplo%20de%20grafo%20relaciones%20de%20seguimiento%20de%20canales%20Youtube.jpg "Figura4.1_Ejemplo de grafo relaciones de seguimiento de canales Youtube"
[figura4.2]: images/Figura4.2_Grafo%20de%20co-ocurrencia%20de%20hashtags.jpg "Figura4.2_Grafo de co-ocurrencia de hashtags"
[figura4.3]: images/Figura4.3_Visualizaci√≥n%20del%20grafo%20de%20co-ocurrencia%20de%20hashtags.jpg "Figura4.3_Visualizaci√≥n del grafo de co-ocurrencia de hashtags"
[figura4.4]: images/Figura4.4_Visualizaci√≥n%20del%20grafo%20de%20ciudades.png "Figura4.4_Visualizaci√≥n del grafo de ciudades"
[figura4.5]: images/Figura4.5_Visualizaci√≥n%20de%20un%20grafo%20social.png "Figura4.5_Visualizaci√≥n de un grafo social"
[ecuaci√≥n4.1]: images/Ecuacion4.1_Cercania.jpg "Ecuaci√≥n4.1 Cercania"
[ecuaci√≥n4.2]: images/Ecuacion4.2_Intermediacion.jpg "Ecuaci√≥n4.2 Intermediacion"
[ecuaci√≥n4.3]: images/Ecuacion4.3_Coeficiente%20local.jpg "Ecuaci√≥n4.3 Coeficiente local"
[ecuaci√≥n4.4]: images/Ecuacion4.4_Vecinos%20comunes.jpg "Ecuaci√≥n4.4 Vecinos comunes"
[ecuaci√≥n4.5]: images/Ecuacion4.5_Adhesi√≥n%20preferencial.jpg "Ecuaci√≥n4.5 Adhesi√≥n preferencial"
[ecuaci√≥n4.6]: images/Ecuacion4.6_Asignaci√≥n%20de%20recursos.jpg "Ecuaci√≥n4.6 Asignaci√≥n de recursos"
